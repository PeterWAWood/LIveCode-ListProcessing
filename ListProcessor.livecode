script "ListProcessor"

##  Simple list processing functions
##
##  Author: Peter W A Wood
##
##  © Copyright 2015 Peter W A Wood
##
##  This software can be freely used under the terms of the GNU General Public License
##  version 3. If you do not have a copy of that license, there is a copy at:
##    https://github.com/PeterWAWood/LiveCode-ListProcessor/blob/master/LICENSE

### Handlers
on closeStack
end closeStack

### local functions
private function inBounds? @pList, idx
  if idx < first item of extents(pList["items"]) then return false
  if idx > second item of extents(pList["items"]) then return false
  return true
end inBounds?

private function list? @pList
  if pList is empty then return false
  if pList is not an array then return false
  if pList["index"] is not a number then return false
  if plist["items"] is not an array then return false
  return true
end list?

### published commands and functions

## LP.head - sets the index to the first item and returns the first item
##    pList         The list
##    returns       The first item in the list or empty
function LP.head @pList
  if not list?(pList) then return empty
  if pList["index"] = 0 then return empty
  put 1 into pList["index"]
  return pList["items"][pList["index"]]
end LP.head

## LP.head? - returns true if the index is set to the first item in the list
##    pList         The list
##    returns       True or false
function LP.head? @pList
  if not list?(pList) then return false
  if pList["index"] = 0 then return false
  if pList["index"] = 1 then return true
  return false
end LP.head?

## LP.index? - returns the value of the current index
##    pList         The list
##    returns       The value of the current index
function LP.index? @pList
  return plist["index"]
end LP.index?

## LP.length? - returns the number of items in the list based on the current index
##    pList         The list
##    returns       The number of items in the list from the current index
function LP.length? @pList
  local tLen
  if not list?(pList) then return 0
  if pList["index"] = 0 then return 0
  put second item of extents(pList["items"]) into tLen
  return tLen - plist["index"] + 1
end LP.length?

## LP.makeList - creates a list
##    pStringList     The items to put in the list
##    pDelimiter      The separator used in the string between items in the list
##    returns         A list or empty
function LP.makeList pStringList, pDelimiter
  local tList
  if pStringList is empty then
    put empty into tList["items"]
    put 0 into tList["index"]
  else if pDelimiter is not empty then
    put pStringList into tList["items"]
    split tList["items"] by pDelimiter
    put 1 into tList["index"]
  else
    put pStringList into tList["items"][1]
    put 1 into tList["index"]
  end if
  return tList
end LP.makeList

## LP.next - moves to the next item in the list and returns that item or empty
##    pList         The list
##    returns       The now first item or empty
function LP.next @pList
  if not list?(pList) then return empty
  if plist["index"] < LP.length?(pList) then
    add 1 to plist["index"]
    return pList["items"][plist["index"]]
  end if
  return empty
end LP.next

## LP.peek - returns the nth item based on the current index
##    pList         The list
##    pPos          The position of the element requested
##    returns       The item or empty
function LP.peek @pList, pPos
  local atPos
  if not list?(pList) then return empty
  put pList["index"] + pPos - 1 into atPos
  if not inBounds?(pList, atPos) then return empty
  return pList["items"][atPos]
end LP.peek

## LP.poke - puts the supplied value into the nth position based on the current index
##    pList         The list
##    pPos          The position of the element requested
##    pVel          The value to be poked into the list
##    returns       The item poked into the list or empty
function LP.poke @pList, pPos, pVal
  local atPos
  if not list?(pList) then return empty
  put pList["index"] + pPos - 1 into atPos
  if not inBounds?(pList, atPos) then return empty
  put pVal into pList["items"][atPos]
  return pList["items"][atPos]
end LP.poke

## LP.prior - moves to the prior item in the list and returns that item or empty
##    pList         The list
##    returns       The now first item or empty
function LP.prior @pList
  if not list?(pList) then return empty
  if plist["index"] > 1 then
    subtract 1 from plist["index"]
    return pList["items"][plist["index"]]
  end if
  return empty
end LP.prior

## LP.tail - sets the index to the last item and returns the last item
##    pList         The list
##    returns       The last item in the list or empty
function LP.tail @pList
  if not list?(pList) then return empty
  if pList["index"] = 0 then return empty
  put LP.length?(pList) into pList["index"]
  return pList["items"][pList["index"]]
end LP.tail

## LP.tail? - returns true if the index is set to the last item in the list
##    pList         The list
##    returns       True or false
function LP.tail? @pList
  if not list?(pList) then return false
  if pList["index"] = 0 then return false
  if LP.length?(pList) = 1 then return true
  return false
end LP.tail?
